#lang rhombus

export:
  ^
  >..
  >..=

fun
| desc_range(from :: Int, to :: Int, incl :: Boolean) :: Sequence:
    if from >= to
    | Sequence.make(
        ~initial_position: from,
        ~position_to_element: fun(i): i,
        ~position_to_next: fun(i): i - 1,
        ~continue_at_position: fun(i): if incl | i >= to | i > to
      )
    | error("desc_range requires a starting point greater than or equal to the ending point")
| desc_range(from :: Int) :: Sequence:
    Sequence.make(
      ~initial_position: from,
      ~position_to_element: fun(i): i,
      ~position_to_next: fun(i): i - 1
    )
    
operator (a :: Int) >.. (b :: Int):
    ~weaker_than: + - / * ^
    desc_range(a, b, #false)

operator ((a :: Int) >..= (b :: Int)):
  ~weaker_than: + - / * ^
  desc_range(a, b, #true)
    
operator ((x :: Real) ^ (y :: NonnegInt)):
  ~stronger_than: + - / *
  match y
  | 0: 1
  | ~else: x * x ^ (y - 1)

